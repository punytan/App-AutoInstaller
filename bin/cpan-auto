#!/usr/bin/env perl

use strict;
use warnings;
use File::Spec;
use Module::CoreList;
use Config;
use Data::Dumper;

our $VERSION = '0.01';

die 'not supported' if $^O eq 'MSWin32';

our $APP = {
    wget     => which('wget'),
    cpanm    => undef,
    outdated => which('cpan-outdated'),
};

our @CPANM_OPT = qw/--skip-installed --prompt/;

main();

exit;

sub main {
    init();

    my $script = shift @ARGV or die "Specify the script!";

    require Module::ExtractUse;
    my $p = Module::ExtractUse->new;
    $p->extract_use($script);

    my @required = diff($p->array);

    run_cpanm(@required);
    run_outdated();
}

sub init {
    die 'Cannot find wget.' unless ($APP->{wget});
    print "wget: $APP->{wget}\n";

    get_cpanm();
    print "App::cpanminus: $APP->{cpanm}\n";

    run_cpanm('App::cpanoutdated') unless ($APP->{outdated});
    $APP->{outdated} = which('cpan-outdated');
    print "App::cpanoutdated: $APP->{outdated}\n";

    run_cpanm('Module::ExtractUse') unless (eval {require Module::ExtractUse;});
    run_cpanm('List::MoreUtils') unless (eval {require List::MoreUtils;});
}

sub run_outdated {
    my @cmd = ("$APP->{outdated} | $APP->{cpanm}");

    print "\n\t# All old modules will be upgraded. This process takes time. #\n\n";
    system(@cmd) eq '0' ? print "\n\nSuccess\n" : die "Oh, error...";
}

sub run_cpanm {
    my @modules = @_;

    print "Modules listed below (and dependencies) are planned to install.\n\t* ", join("\n\t* ", @modules), "\n\n";

    my $separator = '=' x 30;
    for my $module (@modules) {
        my @cmd = ($APP->{cpanm}, $module, @CPANM_OPT);
        print "$separator\n\t** $module **\n$separator\n";
        system(@cmd) eq '0' ? print "Success\n\n" : die "Oh, error... Retry !\n\n";
    }
}

sub diff {
    my @used = @_;

    require List::MoreUtils;
    my @required;
    push( @required, (grep { !$Module::CoreList::version{"$]"}->{$_}  } List::MoreUtils::uniq( sort @used )));

    return @required;
}

sub get_cpanm {
    my @cmd = ("$APP->{wget} http://cpanmin.us/ -O - | perl - App::cpanminus");

    die 'Cannot install cpanminus.' if system(@cmd) ne '0';

    $APP->{cpanm} = which('cpanm');
}

# taken from cpan-outdated++
sub which {
    my($name) = @_;
    my $exe_ext = $Config{_exe};

    my $quote = q/'/;
    foreach my $dir(File::Spec->path){
        my $fullpath = File::Spec->catfile($dir, $name);
        if (-x $fullpath || -x ($fullpath .= $exe_ext)){
            if ($fullpath =~ /\s/ && $fullpath !~ /^$quote/) {
                $fullpath = "$quote$fullpath$quote"
            }
            return $fullpath;
        }
    }
    return undef;
}

__END__

=cut
